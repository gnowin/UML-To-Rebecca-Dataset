reactiveclass Messenger(5) { 
	knownrebecs { 
		Medium medium;     
	} 
      
	statevars { 
		boolean sendBit; 
		boolean hasSucceeded; 
	} 
  
	Messenger() { 
		sendBit = false;
		self.sendMsg(); 
	} 
	
	msgsrv receive(boolean status){
		hasSucceeded = status;
		self.sendMsg(); 
	}

	msgsrv sendMsg() { 
		if (hasSucceeded == true) { 
			if (sendBit == true) { 
				sendBit = false; 
			} else { 
				sendBit = true; 
			} 
		} 
		medium.pass(sendBit); 
	}
} 
  
reactiveclass Receiver(5) { 
	knownrebecs { 
		Medium medium; 
		Messenger messenger;    
	} 
      
	statevars { 
		boolean messageBit; 
	} 
  
	msgsrv receiveMsg(boolean msgBit) { 
		messageBit =msgBit; 
		messenger.receive(true); 
	} 
} 
  
reactiveclass Medium(5) { 
	knownrebecs { 
		Receiver receiver; 
		Messenger messenger;    
	} 
      
	statevars { 
		boolean passMessage; 
	} 
  
	Medium() { 
		passMessage=true; 
	} 
  
	msgsrv pass(boolean msgBit) { 
		passMessage = ?(true,false); 
		if(passMessage == true) { 
			receiver.receiveMsg(msgBit); 
		} else { 
			messenger.receive(false); 
		} 
	} 
} 
  
main { 
	Messenger messenger(medium):(); 
	Medium medium(receiver, messenger):(); 
	Receiver receiver(medium, messenger):(); 
} 